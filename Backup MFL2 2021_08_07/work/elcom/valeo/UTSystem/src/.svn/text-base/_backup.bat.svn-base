@echo off

rem ## full backup anyway:
set cmd_opts=

set path=%path%;C:\program files\winrar

rem ###########################################################################
rem 'A' - add and replace files
rem '-ac' - clear Archive attribute after compression or extraction
rem '-ao' - add files with Archive attribute set
rem '-agyyyy-mm-dd' - generate archive name using the current date and time
rem '-m3' - compression method: normal
rem '-m5' - compression method: best
rem '-s' - create a solid archive
rem '-r' - recurse subfolders
rem '-ed' - Do not add empty directories
rem 'ri<P>[:<S>]' - Set priority (0-default,1-min..15-max) and sleep time in ms
rem '-x<file>' - exclude the specified file
rem '-x@<listfile>' - exclude files using a specified list file
rem ###########################################################################
set cmdexe=WinRAR
set cmdexe=rar
set cmd=%cmdexe% a -r -m5 -s -ed -ri1
set cmd_name_mask=-agyyyy-mm-dd,hh-mm
rem set cmd_name_mask=

rem ####################################
rem ## PREDEFINED EXCLUDE FILE LISTS: ##
rem ####################################
rem ## Archives: ##
set x_archive=-x*.rar -x*.zip -x*.gz -x*.tgz -x*.bz2 -x*.llb
rem ## Development system output files: ##
set x_devsys_out=-x*.exe -x*._xe -x*.lib -x*.dll -x*.ocx -x*.cdf -x*.ou*
set x_devsys_svn=-x*.svn-base -x.svn
rem ## Development system temporary files: ##
rem ## *.opt is not included in 'x_devsys_tmp', because it stores list of
rem ## open documents in Visual Studio and also user-created folders in the
rem ## workspace window.
set x_devsys_tmp=-x*.~c -x*.~h -x*.c~ -x*.h~ -x*.obj -x*.nidobj -x*.pch -x*.ncb -x*.pdb
set x_devsys_tmp=%x_devsys_tmp% -x*.idb -x*.ilk -x*.aps -x*.plg -x*.~*~
set x_devsys_tmp=%x_devsys_tmp% -x*.bsc -x*.sbr -x*.pbt -x*.pbo -x*.pbi -x*.cnt
set x_devsys_tmp=%x_devsys_tmp% -x*.map -x*.cod -x*.i -x*.a -x*.o -x*.lo -x*.tci
rem ## Documents: ##
set x_doc=-x*.pdf -x*.htm -x*.html
rem ## Internet (web pages) stuff: ##
set x_inet=-x*.htm -x*.html -x*.gif -x*.jpg -x*.jpeg -x*.js -x*.css
rem ## Pictures: ##
set x_picture=-x*.bmp -x*.gif -x*.jpg -x*.jpeg -x*.png
rem ## General temporary stuff: ##
set x_tmp=-x*.log -x*.bak -x*.dat -x*.rdf -x*.vi -x*.tex -x*.fft -x*.err
set x_tmp=%x_tmp% -x*.jbf -x*.prn -x*.sys -x*.tmp -x*.gcp


rem ##########################
rem ## ARCHIVE "VALEO_src": ##
rem ##########################
:archive_VALEO_src
rem ## ARCHIVE FILE: ##
set afile=_backup\VALEO_src_.rar
rem ## BASE DIRECTORY: ##
set basedir=.
rem ## FILES TO ADD: ##
set alist=*
rem ## FILES TO EXCLUDE: ##
set xlist=-x*.pdf -x*.img %x_devsys_tmp% %x_devsys_out% %x_devsys_svn% %x_tmp% %x_archive%

set xlist=%xlist% -x*._xe -x*.scc -x*.srec -x*.lib -x*.exp -x*.bmp
rem set xlist=%xlist% %x_picture%
set xlist=%xlist% -x*.cdb -xolsfw\*.lst -xolsfw\*edundo* -xolsgui\cvibuild.olsgui\* -xolsgui\cvidistkit.olsgui\*
set xlist=%xlist% -xdoxygen\html\*
goto backup


:backup

rem ## concatenate basic command with all options: ##
set cmd=%cmd% %cmd_name_mask% %cmd_opts% %cmd_psw%

rem echo %cmd% %xlist% %afile% %alist%
rem pause

cd %basedir%
rem start %cmd% %xlist% %afile% %alist%
%cmd% %xlist% %afile% %alist%
pause

:end
